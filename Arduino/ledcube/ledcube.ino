//LEDのアノード側接続ピン
int led[] = {3,4,5,6,7,8,9,10,11};
//レイヤーのカソード側接続ピン
int layer[] = {12,13,2};
//アニメーションの切り替え間隔
int cycletime = 200;

// アニメーション用データを格納する配列
int TEMP_LED[] = {
  0x000, 0x000, 0x000}; // 左からトップ層ミドル層ボトム層

/* アニメーション１ LED点灯データ
// 上からTOP MID BOTの３層に分割してLEDの点灯パターンを定義する。
// 1~9番目のLEDのON/OFFを1,0のバイナリ列で表し、１６進数に変換して表記
// 全部OFF 000000000 → 0x000
// 全部ON 111111111 → 0x1FF
*/
int ANIM_1_TOP[] = {
  0x000,0x1FF,0x000,0x000,0x1FF,0x000,0x000,0x1D9,0x000,0x000,
  0x000,0x124,0x010,0x092,0x1FF,0x092,0x1FF,0x092,0x000,0x092,
  0x000,0x000,0x092,0x000,0x000,0x092,0x000,0x000,0x000,0x000
};

int ANIM_1_MID[] = {
  0x000,0x1FF,0x000,0x000,0x1FF,0x000,0x001,0x001,0x000,0x000,
  0x100,0x000,0x000,0x001,0x000,0x000,0x000,0x000,0x100,0x000,
  0x000,0x000,0x000,0x000,0x000,0x000,0x000,0x000,0x000,0x000
};

int ANIM_1_BOT[] = {
  0x000,0x000,0x1FF,0x1FF,0x000,0x001,0x001,0x001,0x000,0x000,
  0x000,0x100,0x000,0x000,0x001,0x000,0x000,0x000,0x000,0x100,
  0x000,0x000,0x000,0x000,0x000,0x000,0x000,0x000,0x000,0x000
};

//アニメーション2 LED点灯データ
int ANIM_2_TOP[] = {
  0x1FF,0x124,0x092,0x049,0x000,0x049,0x054,0x038,0x111,0x092,
  0x145,0x010,0x0AA,0x010,0x080,0x100,0x020,0x004,0x001,0x100,
  0x040,0x004,0x000,0x000,0x000,0x054,0x1FF,0x010,0x000,0x000
};

int ANIM_2_MID[] = {
  0x000,0x092,0x092,0x092,0x000,0x049,0x054,0x038,0x111,0x092,
  0x010,0x145,0x010,0x0AA,0x010,0x010,0x010,0x010,0x001,0x100,
  0x040,0x004,0x000,0x000,0x054,0x000,0x1FF,0x145,0x0AA,0x000
};

int ANIM_2_BOT[] = {
  0x000,0x049,0x092,0x124,0x1ff,0x049,0x054,0x038,0x111,0x092,
  0x145,0x010,0x0aa,0x010,0x002,0x001,0x008,0x040,0x001,0x100,
  0x040,0x004,0x000,0x054,0x000,0x000,0x1ff,0x010,0x000,0x145
};

//全消灯関数　sizeof()はarray.length
void AllOFF(){
    for(int i=0; i < (sizeof(led)/sizeof(int)); i++){
    digitalWrite(led[i], LOW);
  }
  for(int i = 0; i < (sizeof(layer) / sizeof(int)); i++){
    digitalWrite(layer[i], HIGH);
  }
}

//ダイナミック点灯制御
void DynaDrive(int pat[]){
//1msで上下のレイヤーを点灯消灯　配列の先頭のポインタが与えられる
  for(int n = 0; n < cycletime / 3; n++){
    //レイヤー数（３層）を順に点灯する
    for(int L = 0; L < (sizeof(layer) / sizeof(int)); L++){
      for(int temp = 0; temp < (sizeof(led) / sizeof(int)); temp++){
        //配列中の該当LEDのビットが1なら高電位にセット
        if(bitRead(pat[L], temp) == B01){
          digitalWrite(led[temp], HIGH);
        }
        else{
          digitalWrite(led[temp], LOW);
        }
      }
      //点灯するLEDの回路に電位差を設定する。
      digitalWrite(layer[L], LOW);
      delayMicroseconds(500);
      digitalWrite(layer[L], HIGH);
      delayMicroseconds(500);
    }
  }
}

//起動時処理
void setup(){
  //LEDに接続した全ピンを初期化
  for(int i = 0; i < (sizeof(led) / sizeof(int)); i++){
    pinMode(led[i], OUTPUT);
  }
  for(int i =0; i < (sizeof(layer) / sizeof(int)); i++){
    pinMode(layer[i], OUTPUT);
  }
  //LEDを全消灯
  AllOFF();
}

//繰り返し処理
void loop(){
  //アニメーション１を一周描画する
  for(int n = 0; n < (sizeof(ANIM_1_TOP) / sizeof(int)); n++){
    TEMP_LED[0] = ANIM_1_TOP[n];
    TEMP_LED[1] = ANIM_1_MID[n];
    TEMP_LED[2] = ANIM_1_BOT[n];
    DynaDrive(TEMP_LED);
    AllOFF();
  }
  //アニメーション2を一周描画する
  for(int n = 0; n < (sizeof(ANIM_2_TOP) / sizeof(int)); n++){
    TEMP_LED[0] = ANIM_2_TOP[n];
    TEMP_LED[1] = ANIM_2_MID[n];
    TEMP_LED[2] = ANIM_2_BOT[n];
    DynaDrive(TEMP_LED);
    AllOFF();
  }
}
